#!/usr/bin/env perl

use v5.24;
use warnings;
use experimental qw( signatures );

use Capture::Tiny qw( tee );
use File::chdir;
use FindBin qw( $Bin );
use Memoize;
use Path::Tiny;
use Term::ANSIColor qw( colored );
use YAML::XS qw( LoadFile );

use constant DATASET => $ENV{ADVENT_DATASET} || 'test';

sub path_to_year ( $year ) {
	my $path = Path::Tiny::->new( $Bin )->absolute->child( $year );
	$path->is_dir or die;
	return $path;
}

sub path_to_day ( $year, $day ) {
	my $path = path_to_year( $year )->child( $day );
	$path->is_dir or die;
	return $path;
}

sub path_to_solution ( $year, $day, $solution ) {
	my $path = path_to_day( $year, $day )->child( $solution );
	$path->is_dir or die;
	return $path;
}

memoize 'get_config';
sub get_config ( $year, $day ) {
	return LoadFile( path_to_day( $year, $day )->child( "day.yml" ) );
}

sub run_solution ( $year, $day, $solution, $env = DATASET ) {
	my $config = get_config( $year, $day );

	my $name = "$year/$day/$solution ($env)";
	if ( $config->{solutions}{$solution}{slow} and not $ENV{ADVENT_SLOW} ) {
		print colored( [ "magenta" ], "$name - skipped (slow)" ), "\n";
		return;
	}
	print colored( [ "bold yellow" ], $name ), "\n";

	my %env = %{ $config->{env}{$env} or die };
	$env{ADVENT_INPUT} = path_to_day( $year, $day )->child( $env{ADVENT_INPUT} )->absolute;
	%ENV = ( %ENV, %env );

	my ( $stdout, $stderr ) = tee {
		local $CWD = path_to_solution( $year, $day, $solution )->stringify;
		if ( -f 'Makefile' ) {
			system( qw( make ) );
			system( qw( make clean ) );
		}
		elsif ( -f 'Cargo.toml' ) {
			system( qw( cargo run ) );
			system( qw( cargo clean ) );
		}
	};

	my $results = { 'NAME' => $name };
	my %expect = %{ $config->{expect}{$env} or die };
	for my $key ( sort keys %expect ) {
		my $look_for = quotemeta( $key . ': ' . $expect{$key} );
		$results->{$key} = !!( $stdout =~ /$look_for/ );
	}
	return $results;
}

sub run_day ( $year, $day, $env = DATASET ) {
	my $config = eval { get_config( $year, $day ) }
		or return;
	map {
		run_solution( $year, $day, $_, $env )
	} sort keys %{ $config->{solutions} or die };
}

sub run_year ( $year, $env = DATASET ) {
	my $y = path_to_year( $year );
	my @days = sort map $_->basename, grep /(\d+)/, $y->children;
	map { run_day( $year, $_, $env ) } @days;
}

sub run_all ( $env = DATASET ) {
	(
		run_year( '2022', $env ),
	);
}

sub run_cli ( @argv ) {

	my ( $YYYY, $DD, $SOLUTION ) = @argv;
	my @results;

	if ( defined $SOLUTION ) {
		@results = run_solution( $YYYY, $DD, $SOLUTION );
	}
	elsif ( defined $DD ) {
		@results = run_day( $YYYY, $DD );
	}
	elsif ( defined $YYYY ) {
		@results = run_year( $YYYY );
	}
	else {
		@results = run_all();
	}

	print "\n";
	my $fails = 0;
	for my $result ( @results ) {
		my $name = delete $result->{NAME};
		my @keys = sort keys %$result;
		my $joined = join " ", map {
			++$fails if !$result->{$_};
			colored( $result->{$_} ? ["green"] : ["red"], $_ )
		} @keys;
		printf(
			"%-48s  %s\n",
			colored( [ "bold white" ], "$name:" ),
			$joined,
		);
	}

	if ( $fails ) {
		print colored( [ "bold red" ], "Failed $fails tests!" ), "\n";
	}

	return $fails;
}

exit( run_cli( @ARGV ) ) unless caller;
